set(IRODS_POLICY_NAME "elasticsearch")
set(IRODS_PLUGIN_TARGET_NAME "${IRODS_TARGET_NAME_PREFIX}-${IRODS_POLICY_NAME}")

string(REPLACE "_" "-" IRODS_POLICY_NAME_HYPHENS "${IRODS_POLICY_NAME}")
set(IRODS_POLICY_PACKAGE_COMPONENT "${IRODS_POLICY_NAME_HYPHENS}")

include(ObjectTargetHelpers)

add_library(
	"${IRODS_PLUGIN_TARGET_NAME}"
	MODULE
	"${CMAKE_CURRENT_SOURCE_DIR}/src/main.cpp"
)
target_link_objects(
	"${IRODS_PLUGIN_TARGET_NAME}"
	PRIVATE
	irods_cap_indexing_common_obj
)
target_link_libraries(
	"${IRODS_PLUGIN_TARGET_NAME}"
	PRIVATE
	irods_common
	irods_server
	nlohmann_json::nlohmann_json
	"${IRODS_EXTERNALS_FULLPATH_BOOST}/lib/libboost_system.so"
	"${IRODS_EXTERNALS_FULLPATH_BOOST}/lib/libboost_url.so"
	"${IRODS_EXTERNALS_FULLPATH_FMT}/lib/libfmt.so"
)
target_include_directories(
	"${IRODS_PLUGIN_TARGET_NAME}"
	PRIVATE
	"${IRODS_EXTERNALS_FULLPATH_BOOST}/include"
	"${IRODS_EXTERNALS_FULLPATH_FMT}/include"
)
target_compile_definitions(
	"${IRODS_PLUGIN_TARGET_NAME}"
	PRIVATE
	BOOST_SYSTEM_NO_DEPRECATED
	RODS_SERVER
	ENABLE_RE
	${IRODS_COMPILE_DEFINITIONS}
	${IRODS_COMPILE_DEFINITIONS_PRIVATE}
	IRODS_PLUGIN_VERSION="${IRODS_PLUGIN_VERSION}"
)
target_compile_options(
	"${IRODS_PLUGIN_TARGET_NAME}"
	PRIVATE
	-Wno-write-strings
)

install(
	TARGETS
	"${IRODS_PLUGIN_TARGET_NAME}"
	LIBRARY
	DESTINATION "${IRODS_PLUGINS_DIRECTORY}/rule_engines"
	COMPONENT "${IRODS_POLICY_PACKAGE_COMPONENT}"
)

string(TOUPPER "${IRODS_POLICY_PACKAGE_COMPONENT}" IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE)
string(REPLACE "_" "-" IRODS_PLUGIN_TARGET_NAME_HYPHENS "${IRODS_PLUGIN_TARGET_NAME}")

# CPack variables need to be set with PARENT_SCOPE, so they will be in scope for CPack.

set(CPACK_DEBIAN_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_PACKAGE_NAME "${IRODS_PLUGIN_TARGET_NAME_HYPHENS}" PARENT_SCOPE)
set(CPACK_DEBIAN_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_PACKAGE_DEPENDS "irods-server (= ${IRODS_VERSION}), irods-runtime (= ${IRODS_VERSION}), libc6" PARENT_SCOPE)
set(CPACK_DEBIAN_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_PACKAGE_CONTROL_EXTRA "${CMAKE_CURRENT_SOURCE_DIR}/packaging/postinst;" PARENT_SCOPE)

set(CPACK_RPM_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_PACKAGE_NAME "${IRODS_PLUGIN_TARGET_NAME_HYPHENS}" PARENT_SCOPE)
set(CPACK_RPM_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_PACKAGE_REQUIRES "irods-server = ${IRODS_VERSION}, irods-runtime = ${IRODS_VERSION}" PARENT_SCOPE)
set(CPACK_RPM_${IRODS_POLICY_PACKAGE_COMPONENT_UPPERCASE}_POST_INSTALL_SCRIPT_FILE "${CMAKE_CURRENT_SOURCE_DIR}/packaging/postinst" PARENT_SCOPE)
